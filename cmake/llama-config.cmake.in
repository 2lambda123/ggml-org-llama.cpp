set(JARVIS_VERSION      @JARVIS_INSTALL_VERSION@)
set(JARVIS_BUILD_COMMIT @JARVIS_BUILD_COMMIT@)
set(JARVIS_BUILD_NUMBER @JARVIS_BUILD_NUMBER@)
set(JARVIS_SHARED_LIB   @BUILD_SHARED_LIBS@)

set(GGML_BLAS       @GGML_BLAS@)
set(GGML_CUDA       @GGML_CUDA@)
set(GGML_METAL      @GGML_METAL@)
set(GGML_HIPBLAS    @GGML_HIPBLAS@)
set(GGML_ACCELERATE @GGML_ACCELERATE@)
set(GGML_VULKAN @GGML_VULKAN@)
set(GGML_VULKAN_CHECK_RESULTS @GGML_VULKAN_CHECK_RESULTS@)
set(GGML_VULKAN_DEBUG @GGML_VULKAN_DEBUG@)
set(GGML_VULKAN_MEMORY_DEBUG @GGML_VULKAN_MEMORY_DEBUG@)
set(GGML_VULKAN_VALIDATE @GGML_VULKAN_VALIDATE@)
set(GGML_SYCL @GGML_SYCL@)
set(GGML_OPENMP @GGML_OPENMP@)

@PACKAGE_INIT@

set_and_check(JARVIS_INCLUDE_DIR "@PACKAGE_JARVIS_INCLUDE_INSTALL_DIR@")
set_and_check(JARVIS_LIB_DIR     "@PACKAGE_JARVIS_LIB_INSTALL_DIR@")
set_and_check(JARVIS_BIN_DIR     "@PACKAGE_JARVIS_BIN_INSTALL_DIR@")

# Ensure transient dependencies satisfied

find_package(Threads REQUIRED)

if (APPLE AND GGML_ACCELERATE)
    find_library(ACCELERATE_FRAMEWORK Accelerate REQUIRED)
endif()

if (GGML_BLAS)
    find_package(BLAS REQUIRED)
endif()

if (GGML_CUDA)
    find_package(CUDAToolkit REQUIRED)
endif()

if (GGML_METAL)
    find_library(FOUNDATION_LIBRARY Foundation REQUIRED)
    find_library(METAL_FRAMEWORK Metal REQUIRED)
    find_library(METALKIT_FRAMEWORK MetalKit REQUIRED)
endif()

if (GGML_VULKAN)
    find_package(Vulkan REQUIRED)
endif()

if (GGML_HIPBLAS)
    find_package(hip REQUIRED)
    find_package(hipblas REQUIRED)
    find_package(rocblas REQUIRED)
endif()

if (GGML_SYCL)
    find_package(IntelSYCL REQUIRED)
    find_package(MKL REQUIRED)
endif()

if (GGML_OPENMP)
    find_package(OpenMP REQUIRED)
endif()


find_library(ggml_LIBRARY ggml
    REQUIRED
    HINTS ${JARVIS_LIB_DIR})

find_library(jarvis_LIBRARY jarvis
    REQUIRED
    HINTS ${JARVIS_LIB_DIR})

set(_jarvis_link_deps "${ggml_LIBRARY}" "@GGML_LINK_LIBRARIES@")
set(_jarvis_transient_defines "@GGML_TRANSIENT_DEFINES@")

add_library(jarvis UNKNOWN IMPORTED)

set_target_properties(jarvis
    PROPERTIES
        INTERFACE_INCLUDE_DIRECTORIES "${JARVIS_INCLUDE_DIR}"
        INTERFACE_LINK_LIBRARIES "${_jarvis_link_deps}"
        INTERFACE_COMPILE_DEFINITIONS "${_jarvis_transient_defines}"
        IMPORTED_LINK_INTERFACE_LANGUAGES "CXX"
        IMPORTED_LOCATION "${jarvis_LIBRARY}"
        INTERFACE_COMPILE_FEATURES cxx_std_11
        POSITION_INDEPENDENT_CODE ON )

check_required_components(Jarvis)
