#version 450

#extension GL_KHR_shader_subgroup_arithmetic: enable

#ifdef FLOAT16
#extension GL_EXT_shader_explicit_arithmetic_types_float16 : require
#endif

#include "mul_mat_vec_base.comp"

layout(local_size_x_id = 0, local_size_y = 1, local_size_z = 1) in;

layout (constant_id = 0) const uint BLOCK_SIZE = 32;

void main() {
    const uint row = gl_WorkGroupID.x + gl_NumWorkGroups.x * gl_WorkGroupID.z;
    const uint tid = gl_LocalInvocationID.x;

    // There are not enough cols to use all threads
    if (tid >= p.ncols) {
        return;
    }

    const uint block_size = min(p.ncols, BLOCK_SIZE);

    uint a_offset, b_offset, d_offset;
    get_offsets(a_offset, b_offset, d_offset);

    const uint y_offset = QUANT_R == 1 ? 1 : QUANT_K/2;

    FLOAT_TYPE tmp = FLOAT_TYPE(0.0f);

    [[unroll]] for (uint i = 0; i < p.ncols/block_size; i += 2) {
        const uint col = i*block_size + 2*tid;
        const uint ib = (row*p.ncols + col)/QUANT_K; // block index
        const uint iqs = (col%QUANT_K)/QUANT_R; // quant index
        const uint iybs = col - col%QUANT_K; // y block start index

        vec2 v = dequantize(ib, iqs, a_offset / QUANT_K);

        // matrix multiplication
        tmp = fma(FLOAT_TYPE(v.x), FLOAT_TYPE(data_b[b_offset + iybs + iqs]), fma(FLOAT_TYPE(v.y), FLOAT_TYPE(data_b[b_offset + iybs + iqs + y_offset]), tmp));
    }

    // sum up partial sums and write back result
    tmp = subgroupAdd(tmp);
    if (tid == 0)
        data_d[d_offset + row] = D_TYPE(tmp);
}
